buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
        classpath 'com.ofg:uptodate-gradle-plugin:1.0.0'
    }
}

apply plugin: 'bintray'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.ofg.uptodate'

group = 'com.ofg'
description = 'Gradle plugin that adds run/stub mocks tasks and proper configurations to your project'
version = '0.0.3-SNAPSHOT'

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'

    testCompile 'junit:junit:4.11'
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'com.github.tomakehurst:wiremock:1.47'
    testRuntime 'cglib:cglib-nodep:3.1'
    testRuntime 'org.objenesis:objenesis:2.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

bintrayUpload.dependsOn 'build'

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.properties['bintrayUser']
    key = System.properties['bintrayKey']
    publications = ['plugin']
    pkg {
        repo = 'micro'
        userOrg = '4finance' 
        name = project.name
        desc = project.description
        licenses = ['Apache-2.0']
        labels = ['gradle', 'microservice']
    } 
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

groovydoc {
    source = sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
